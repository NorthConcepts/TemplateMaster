
plugins {
    id 'java-library'
    id 'maven-publish'
}

archivesBaseName='NorthConcepts-TemplateMaster'.toLowerCase()

repositories {
    mavenLocal()
    jcenter()
}

compileJava {
    options.compilerArgs += ["-parameters", "-g:lines,vars,source"]
}

publishing {
    publications {

        templatemaster(MavenPublication) {

            groupId = "${mavenTemplateMasterGroupId}"
            version = "${version}"
            artifactId = "${archivesBaseName}"

            from components.java
            artifact sourceJar
            artifact packageJavadoc
        }

    }

    repositories {
        maven {
            credentials {
                username "${mavenUser}"
                password "${mavenPassword}"
            }
            name = "${mavenTemplateMasterRepo}"
            url = "${mavenTemplateMasterUrl}"
        }
    }

}

dependencies {
    api 'org.freemarker:freemarker:2.3.28'
    api 'org.jboss.resteasy:resteasy-jaxrs:3.6.1.Final'
    api 'javax.servlet:javax.servlet-api:4.0.1'
    api 'org.apache.logging.log4j:log4j-core:2.11.1'
    api 'org.jboss.resteasy:resteasy-jackson2-provider:3.6.1.Final'
    api 'com.fasterxml.jackson.core:jackson-databind:2.9.7'
    api 'org.springframework.data:spring-data-commons:2.1.2.RELEASE'
    api 'com.fasterxml.jackson.core:jackson-core:2.9.9'
    api 'org.apache.commons:commons-text:1.6'

    testImplementation 'junit:junit:4.12'
}

javadoc {
  source = sourceSets.main.allJava
  classpath = configurations.compileClasspath

  options {
    setMemberLevel JavadocMemberLevel.PUBLIC
    setAuthor true

    links "https://docs.oracle.com/javase/8/docs/api/"
  }
}

task sourceJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allJava
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}
